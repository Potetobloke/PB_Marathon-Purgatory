/*

	-
	
*/

Class MarFusionBattery_Ammo : Ammo
{
    Default
    {
		Inventory.PickupMessage "[Fusion Battery]";
		Inventory.Amount 20;
		Inventory.MaxAmount 600;
		Ammo.DropAmount 20;
		Ammo.BackpackAmount 20;
		Ammo.BackpackMaxAmount 600;
		//Ammo.BackpackMaxAmount 700;
		Inventory.PickUpSound "Marathon/Items/Ammo";
		Inventory.Icon "";
	}
	States
    {
    Spawn:
		ZCFP Y -1;
		Stop;
	}
}

class MarFusionPistol_Weapon : MarathonWeapon
{

	int charging;
	int altfiretimer;
	
    Default
    {
		ZWeapon.MagazineSize 20;
        Weapon.SelectionOrder 1000;
        Weapon.AmmoUse1 1;
        Weapon.AmmoGive1 20;
        Weapon.AmmoType1 "MarFusionBattery_Ammo";
		Weapon.WeaponScaleX 1.35;
		Weapon.WeaponScaleY 1.35;
		
		Inventory.PickupMessage "( Zeus Class Fusion Pistol )";
		
		Tag "Zeus Class Fusion Pistol";
    }



    States
    {
	RecoilShit:
		TNT1 A 1 PB_RecoilReset(2,1,1);
		loop;
    Ready:
		ZCFP A 0 A_JumpIf(invoker.ammocount>invoker.magazinesize, "Reload.instant");
		ZCFP A 0 
		{
			A_Overlay(29,"RecoilShit");
			A_WeaponOffset(0,32,WOF_INTERPOLATE);
			invoker.charging=0;
			invoker.altfiretimer=0;
			invoker.recoiloffset_base=0;
		}
        ZCFP A 1 ZWL_WeaponReady(ZRF_NoPartialReload);
        Loop;

    Deselect:
		#### # 0 A_JumpIf(health <= 0, "Dead.Ass");
		ZCFP A 0 A_ClearOverlays(29,31);
        ZCFP A 1 A_Lower(5);
        Wait;
    Select:	
        ZCFP A 1 A_Raise(5);
        Wait;
		
	Fire:
		ZCFP B 0 A_JumpIf(waterlevel == 3, "Fire.Fail");
		ZCFP B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
		ZCFP A 1 bright 
		{
			A_AlertMonsters();
			PB_RecoilAnim(PSP_WEAPON,3);
			A_StartSound("marathon/weapons/fusionpistol/fire",CHAN_WEAPON,CHANF_OVERLAP);
			ZWL_FireProjectile("MarFusion_Projectile",0,-1);
		}
		ZCFP BB 1 bright PB_RecoilAnim(PSP_WEAPON,7);
		ZCFP CC 1 PB_RecoilAnim(PSP_WEAPON,7);
		ZCFP C 1;
		ZCFP D 3;
		ZCFP A 1;
		ZCFP A 0 ZWL_JumpIfEmpty("Fire.End");
		ZCFP AAA 1 ZWL_Refire("Fire");
		ZCFP A 0 {invoker.recoiloffset_base=0;}
        Goto Ready;
	Fire.End:
		ZCFP A 1 {A_ClearOverlays(31,31);A_StopSound(CHAN_7);invoker.charging=0;}
		Goto Ready;
	Fire.Fail:
		ZCFP A 8;
		Goto Ready;
	Reload:
		ZCFP # 0 A_ClearOverlays(29,31);
		ZCFP ############# 1 A_Lower(5);
		ZCFP # 32;
		ZCFP A 0 ZWL_Reload();
		ZCFP ############# 1 A_Raise(5);
		Goto ready;
		
	//Charged Shot
	
	Die.Right.Now:
		ZCFP A 1 
			{
				A_StartSound("marathon/weapons/fusionpistol/boom",CHAN_BODY,CHANF_OVERLAP,pitch:1.1);
				A_SetBlend("ff ff ff",1.,256);
				DamageThing(500,0);
			}
		stop;
		
	Altfire:
		ZCFP A 0 A_JumpIf(invoker.ammoCount>=4, "Alt");
		goto ready;
	Alt:
		ZCFP A 0 A_Overlay(31,"beeping");
		ZCFP A 1 A_StartSound("marathon/weapons/fusionpistol/charge",CHAN_7);
	AltHold:
		ZCFP A 1;
		ZCFP A 0 A_JumpIf(invoker.charging>0, "AltHolding");
		ZCFP A 0 {A_WeaponOffset(0,32,WOF_INTERPOLATE);ZWL_Refire("AltHold");}
		goto fire.end;
	AltHolding:
		ZCFP A 1 
		{
			let crg = invoker.charging*0.05;
			A_WeaponOffset((frandom(-1,1))*crg,(frandom(-1,1))*crg,WOF_ADD|WOF_INTERPOLATE);
			if(invoker.charging>45){PB_SetWeaponState("Die.Right.Now");}
		}
		ZCFP A 0 {A_WeaponOffset(0,32,WOF_INTERPOLATE);ZWL_Refire("AltHolding");}
	AltShot:
		ZCFP B 0 A_JumpIf(waterlevel == 3, "Fire.Fail");
		ZCFP B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
		ZCFP A 1 bright 
		{
			A_ClearOverlays(31,31);
			invoker.charging=0;
			invoker.altfiretimer=0;
			A_AlertMonsters();
			PB_RecoilAnim(PSP_WEAPON,5);
			A_StartSound("marathon/weapons/fusionpistol/fire2",CHAN_WEAPON,CHANF_OVERLAP);
			ZWL_FireProjectile("MarFusionCharged_Projectile",0,-1,flags:ZPF_DontUseAmmo);
			invoker.ammoCount-=4;
		}
		ZCFP BB 1 bright PB_RecoilAnim(PSP_WEAPON,7);
		ZCFP CC 1 PB_RecoilAnim(PSP_WEAPON,7);
		ZCFP C 1;
		ZCFP D 3;
		ZCFP A 1;
		ZCFP A 0 ZWL_JumpIfEmpty("Fire.End");
		ZCFP AAA 1 ZWL_Refire("Altfire");
		ZCFP A 0 {invoker.recoiloffset_base=0;}
        Goto Ready;
	Beeping:
		TNT1 A 24;
		ZCFP F 16 bright //45 beeps max
		{
			let crg = invoker.charging*0.01;
			if(invoker.charging>0)A_StartSound("marathon/weapons/fusionpistol/beep",CHAN_6,pitch:(1.0+crg));
			invoker.charging++;
		}
		loop;
	Spawn:
        ZCFP Z -1;
        Stop;
    }
}

class MarFusion_Projectile : ZExplosive
{
    Default
    {
        Health 25;
		Radius 8;
		Height 12;
		Speed 25;
		Scale 0.35;
		DamageType "LightningBlue"; //drla compat
		DamageFunction (random(30,40));
		Obituary "$OB_MPROCKET";

		//copying some properties from samsara because these are good
		+FoilInvul //might be too powerful
		+PierceArmor
		+DontReflect
		
        +ForceXYBillboard
		+HitTracer
		+BloodlessImpact
		-NoBlockMap
		+NOEXTREMEDEATH
    }

	States
	{
      Spawn:
        ZFP0 ABC 2 Bright;
        Loop;
       Death:
		ZFP0 D 2 Bright A_StartSound("marathon/weapons/fusionpistol/hit",CHAN_WEAPON,CHANF_OVERLAP);
		ZFP0 EE 1 Bright 
		{
			//A_StartSound("marathon/weapons/fusionpistol/hit",CHAN_WEAPON,CHANF_OVERLAP);
			if(tracer){
			tracer.A_SetBlend("ff ff ff",0.1,32);
			tracer.A_SpawnItemEx((tracer.bSpriteFlip==true)?"MarFusionHit_Flipped":"MarFusionHit", 
			flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
			}
		}
		ZFP0 FFGGZZ 1 Bright 
		{if(tracer)
			{if(tracer.bCorpse){
			tracer.A_SpawnItemEx((tracer.bSpriteFlip==true)?"MarFusionHit_Flipped":"MarFusionHit", 
			flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
				}
			}
		}
        //ZFP0 FG 2 Bright;
        Stop;
	}
}

class MarFusionCharged_Projectile : MarFusion_Projectile
{
    Default
    {
		Radius 12;
		Height 16;
		Speed 35;
		DamageFunction (random(90,120));
		Scale 0.75;
		+FoilInvul
		+FoilBuddha
		+EXTREMEDEATH
		-NOEXTREMEDEATH
    }

	States
	{
      Spawn:
        ZFP1 AAABBBCCC 1 Bright A_SpawnItemEx("MarFusion_Trail");
        Loop;
       Death:
		ZFP0 D 1 Bright 
		{
			A_StartSound("marathon/weapons/fusionpistol/hit",CHAN_WEAPON,CHANF_OVERLAP,pitch:0.9);
			{if(tracer)
				
				{if(tracer.bIsMonster & tracer.bCorpse){
				tracer.A_SpawnItemEx("MarFusionKill", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
				tracer.A_SetBlend("ff ff ff",1.,128);
				tracer.A_Explode(35+((tracer.spawnhealth())*1.5),
				((tracer.radius)*2.5));}
					//i want to make the damage be whatever the max health of what 
					//it killed, but I have NO clue how to do that
			else 
			tracer.A_SetBlend("ff ff ff",0.25,64);
			tracer.A_SpawnItemEx((tracer.bSpriteFlip==true)?"MarFusionHit_Flipped":"MarFusionHit", 
			flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);}
			}
		}
		ZFP0 DEEFF 1 Bright 
		{if(tracer)
			{if(tracer.bCorpse){
			tracer.A_SpawnItemEx((tracer.bSpriteFlip==true)?"MarFusionHit_Flipped":"MarFusionHit", 
			flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
				}
			}
		}
		ZFP0 GGZZZZZ 1 Bright 
		{if(tracer)
			{if(tracer.bCorpse){
			tracer.A_SpawnItemEx((tracer.bSpriteFlip==true)?"MarFusionHit_Flipped":"MarFusionHit", 
			flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
				}
			}
		}
		stop;
	}
}

class MarFusionHit : Actor
{
	default
	{
		Alpha 1.0;
		RenderStyle "Stencil";
		//StencilColor "#ff1100";
		StencilColor "#ffffff";
		+NOINTERACTION
	}

	states
	{
	Spawn:
		#### # 0 NoDelay A_SetScale(scalex*1.1,scaley*1.1);
		#### # 1 Bright {A_FadeOut(0.25);A_SetScale(scalex*1.02,scaley*1.02);A_SpriteOffset(frandom(-1,1),frandom(-1,1));}
		wait;
	}
}

class MarFusionHit_Flipped : MarFusionHit
{
	default
	{}
	states
	{
	Spawn:
		#### # 0 NoDelay {bspriteflip=true;}
		#### # 0
		{
			A_SetRenderStyle(1.0,STYLE_Stencil);
			SetShade("#ffffff");
		}
		#### # 1 Bright {A_FadeOut(0.25);A_SetScale(scalex*1.02,scaley*1.02);A_SpriteOffset(frandom(-1,1),frandom(-1,1));}
		wait;
	}
}
class MarFusionKill : Actor
{
	default
	{
		Alpha 1.0;
		RenderStyle "Stencil";
		//StencilColor "#ff1100";
		StencilColor "#ffffff";
		+NOINTERACTION
	}

	states
	{
	Spawn:
		#### # 0 NoDelay 
		{A_SetScale(scalex*1.1,scaley*1.1);
		A_StartSound("marathon/weapons/fusionpistol/boom",CHAN_BODY,CHANF_OVERLAP,pitch:1.1);}
		#### ##### 1 Bright 
		{
			A_SetScale(scalex*1.1,scaley*0.99);
			A_FadeOut(0.1);
		}
		#### # 1 Bright {A_FadeOut(0.2);A_SetScale(scalex*1.1,scaley*0.99);A_SpriteOffset(0,-1);
		A_SpawnItemEx("MarFusionHit", 
			flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
			}
		wait;
	}
}
class MarFusion_Trail : Actor
{
	Default
	{
		+DONTSPLASH;
		Radius 1;
		Height 1;
		Speed 0;
		+NoBlockmap;
		+NoGravity;
		+FORCEXYBILLBOARD;
		+CLIENTSIDEONLY;
		+RollSprite
		+RollCenter
		+DontBlast

		Scale 0.85;
	}
	
		States
	{
	Spawn:
		TNT1 A 2;
		ZFP2 ABC 2 bright;
		stop;
	}
}

class MarAshPile : Actor
{
    Default
    {
		Height 4;
		Radius 2;

		+Dropoff
		+NoBlockMap
		+MoveWithSector
		+ThruActors
    }
		States
	{
	Spawn:
		ZFP3 Z 0 nodelay A_SetScale(scalex*0.2,scaley*3.5);
		ZFP3 ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ 1 A_SetScale(scalex*1.02,scaley*0.98);
		ZFP3 ZZZZZZZZZZZZZ 1 A_SetScale(scalex*1.01,scaley*0.99);
		ZFP3 Z -1;
		stop;
	}
}

